import os
import yaml
from os import listdir
from os.path import isfile, join
import pathlib

config_download_path = f"{SNAKEMAKE_DIR}/download/config_download.yaml"
with open(config_download_path, "r") as fd:
    config_download = yaml.safe_load(fd)

config_pred_path = f"{SNAKEMAKE_DIR}/splicing_pred/config_pred.yaml"
with open(config_pred_path, "r") as fd:
    config_pred = yaml.safe_load(fd)

configfile: "./config.yaml"

genome = config['genome']
vcf_ids = [file for file in listdir(os.path.dirname(config['vcf'])) if '.tbi' not in file]

def splicemap5(wildcards):
    path = Path(config_download['splicemap_dir'])
    splicemaps = [
        path / f'{tissue}_splicemap_psi5_method=kn_event_filter=median_cutoff.csv.gz' 
        for tissue in config['splicemap_tissues']
        ]
    splicemaps = [str(x) for x in splicemaps]
    return splicemaps

def splicemap3(wildcards):
    path = Path(config_download['splicemap_dir'])
    splicemaps = [
        path / f'{tissue}_splicemap_psi3_method=kn_event_filter=median_cutoff.csv.gz' 
        for tissue in config['splicemap_tissues']
        ]
    splicemaps = [str(x) for x in splicemaps]
    return splicemaps


rule mmsplice_splicemap:
    input:
        vcf = config['vcf'],
        fasta = config_download['fasta'][genome]['file'],
        splicemap_5 = splicemap5,
        splicemap_3 = splicemap3
    resources:
        mem_mb = lambda wildcards, attempt: attempt * 32000,
        threads = 4
    output:
        result = config_pred['splicing_pred']['mmsplice_splicemap']
    script:
        "./mmsplice_splicemap.py"


if config['use_rocksdb'] == True:
    genome_mapper = {
        'hg38': 'grch38',
        'hg19': 'grch37',
    }
    rule spliceai:
        resources:
            mem_mb = lambda wildcards, attempt: attempt * 16000,
            threads = 1,
            gpu = 1,
        input:
            vcf = config['vcf'],
            fasta = config_download['fasta'][genome]['file'],
            spliceai_rocksdb = config_download['spliceai_rocksdb'][genome]
        params:
            lookup_only = False,
            genome = genome_mapper[config['genome']]
        conda:
            "../../environment_spliceai_rocksdb.yaml"
        output:
            result = config_pred['splicing_pred']['spliceai']
        script:
            "./spliceai.py"
else:
    genome_mapper = {
        'hg38': 'grch38',
        'hg19': 'grch37',
    }
    rule spliceai:
        resources:
            mem_mb = lambda wildcards, attempt: attempt * 16000,
            threads = 1,
            gpu = 1,
        input:
            vcf = config['vcf'],
            fasta = config_download['fasta'][genome]['file']
        params:
            genome = genome_mapper[config['genome']]
        conda:
            "../../environment_spliceai_rocksdb.yaml"
        output:
            result = config_pred['splicing_pred']['spliceai_vcf']
        shell:
            'spliceai -I {input.vcf} -O {output.result} -R {input.fasta} -A {params.genome}'
    
    rule spliceai_vcf_to_csv:
        input:
            spliceai_vcf = config_pred['splicing_pred']['spliceai_vcf'],
        output:
            spliceai_csv = config_pred['splicing_pred']['spliceai'],
        run:
            from absplice.utils import read_spliceai_vcf
            df = read_spliceai_vcf(input.spliceai_vcf)
            df.to_csv(output.spliceai_csv, index=False)

rule absplice_dna:
    input:
        mmsplice_splicemap = config_pred['splicing_pred']['mmsplice_splicemap'],
        spliceai = config_pred['splicing_pred']['spliceai'],
    output:
        absplice_dna = config_pred['splicing_pred']['absplice_dna']
    script:
        "./absplice_dna.py"


rule all_predict_dna:
    input:
        expand(config_pred['splicing_pred']['absplice_dna'],
            genome = config['genome'], vcf_id=vcf_ids),


del splicemap5
del splicemap3